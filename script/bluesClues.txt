// This Pine Script™ code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// © thesuperdave
// credit to © MichelT for VWAP

//@version=5
indicator(title="Blue's Clues", shorttitle="Blue's Clues", overlay=true)

// SETTINGS -----------------------------
displaySet          = display.all - display.price_scale - display.status_line
ema                 = "----------------- EMA ------------------"
bb1                 = "BBands HTF"
bb2                 = "BB Toggle"
bb3                 = "BBands LTF"
vwap                = "----------------- VWAP -----------------"
dwmLevels           = "------------- D/W/M Levels -------------"
sdZoneGroupTxt      = "--------- Supply/Demand Zones ----------"
sdZoneTFSettingTxt  = "-------- S/D Zone TF Settings ----------" 
groupEma            = "------------- 20/50/200 EMA ------------"
groupBb             = "------ BBands - 2 Std. Deviation -------"
groupVwap           = "------- Auto Anchored D/W/M VWAP -------"
groupSD             = "------------- Supply/Demand ------------"
groupSDSettings     = "-------- Supply/Demand Settings --------"
ttBb                = "Shows BBands on LTF"
groupDwmLevels      = "------------- D/W/M Levels -------------"
groupCog            = "+/- COG Alerts"
groupAPlusPanel     = "-------------- A+ Panel  ---------------"

emaColor1           = color.new(color.aqua, 50)
emaColor2           = color.new(color.blue, 50)
emaColor3           = color.new(color.purple, 50)
dVwapColor          = color.new(color.gray, 60)
wVwapColor          = color.new(color.yellow, 60)
mVwapColor          = color.new(color.orange, 60)
yVwapColor          = color.new(color.purple, 60)
globexLevelColor    = color.rgb(120, 123, 134, 17)
dLevelColor         = color.rgb(16, 105, 207, 18)
wLevelColor         = color.rgb(218, 221, 8, 5)
mLevelColor         = color.rgb(255, 153, 0, 23)
bbColor             = color.new(color.gray, 50)

// GLOBALS /  CALCS ----------------------------
Offset(X)=>
    Bar=math.min(time - time[1], time[1] - time[2])
    time + Bar * (1 + X * 1)
f_get_changePercentage(_p1, _p2) => (_p1 - _p2) * 100 / _p2

tf                  = timeframe.period
displayLtf          = timeframe.isintraday and timeframe.multiplier <= 5
displayMtf          = timeframe.isintraday and (timeframe.multiplier >= 6 and timeframe.multiplier <= 60)
displayHtf          = timeframe.isdwm
displayVwapD        = tf == "D"
displayVwapLtf      = displayLtf
isFutures           = syminfo.type == 'futures'
isForex             = syminfo.type == 'forex'

src    = close
outLtf = ta.ema(src, 20)
outMtf = ta.ema(src, 50)
outHtf = ta.ema(src, 200)
dev    = 2.0 * ta.stdev(close, 20)

upperHtf = outLtf + dev
lowerHtf = outLtf - dev
upperLtf = outLtf + dev
lowerLtf = outLtf - dev

shortBbandEntryHtf      = close > upperHtf
longBbandEntryHtf       = close < lowerHtf

MILLIS_IN_DAY   = 86400000
dwmBarTime      = timeframe.isdwm ? time : time("D")

globexBlank         = "\n‏   ‏   ‏   ‏   ‏   ‏   ‏   ‏   ‏   ‏"
dBlank              = "\n‏   ‏   ‏   ‏   ‏   ‏   ‏   ‏   ‏   ‏"
wBlank              = "\n‏   ‏   ‏   ‏   ‏   ‏   ‏   ‏   ‏   ‏"
mBlank              = "‏   ‏   ‏   ‏   ‏   ‏   ‏   ‏   ‏   ‏"

[_gT, _gH, _gL]                         = request.security(syminfo.tickerid, "720", [time[1], high[1], low[1]],  barmerge.gaps_off, barmerge.lookahead_on)
[_dH, _dL, _dC]                         = request.security(syminfo.tickerid, "D", [high[1], low[1], close[1]],  barmerge.gaps_off, barmerge.lookahead_on)
[_wH, _wL, _wC]                         = request.security(syminfo.tickerid, "W", [high[1], low[1], close[1]],  barmerge.gaps_off, barmerge.lookahead_on)
[_mH, _mL, _mC]                         = request.security(syminfo.tickerid, "M", [high[1], low[1], close[1]],  barmerge.gaps_off, barmerge.lookahead_on)

is20and200Bullish       = outLtf > outHtf     
is20and50Bullish        = outLtf > outMtf
is20and200Bearish       = not is20and200Bullish
is20and50Bearish        = not is20and50Bullish

legOutCandleSize    = 2.0

// INPUTS --------------------------------

ltfColor = input(defval = emaColor1, title="20 EMA", inline = ema, group = groupEma)
mtfColor = input(defval = emaColor2, title="50 EMA", inline = ema, group = groupEma)
htfColor = input(defval = emaColor3, title="200 EMA", inline = ema, group = groupEma)

upperHtfColor = input(defval = bbColor, title="Upper HTF Band", inline = bb1, group = groupBb)
lowerHtfColor = input(defval = bbColor, title="Lower HTF Band", inline = bb1, group = groupBb)
showBbLtf     = input.bool(defval = false, title = "Show BB on LTF", inline = bb2, group = groupBb)
upperLtfColor = input(defval = bbColor, title="Upper LTF Band", inline = bb3, group = groupBb)
lowerLtfColor = input(defval = bbColor, title="Lower LTF Band", inline = bb3, group = groupBb)

dvColor = input(defval = dVwapColor, title="D VWAP Color", inline = vwap, group = groupVwap)
wvColor = input(defval = wVwapColor, title="W VWAP Color", inline = vwap, group = groupVwap)
mvColor = input(defval = mVwapColor, title="M VWAP Color", inline = vwap, group = groupVwap)
yvColor = input(defval = yVwapColor, title="Y VWAP Color", inline = vwap, group = groupVwap)

globex_Line_Color   = input.color(defval = globexLevelColor, title = "Daily Level", inline = dwmLevels, group = groupDwmLevels)
d_Line_Color        = input.color(defval = dLevelColor, title = "Daily Level", inline = dwmLevels, group = groupDwmLevels)
w_Line_Color        = input.color(defval = wLevelColor, title = "Weekly Level",  inline = dwmLevels, group = groupDwmLevels)
m_Line_Color        = input.color(defval = mLevelColor, title = "Monthly Level", inline = dwmLevels, group = groupDwmLevels)
showGlobexLevels    = input.bool(defval = true, title = "Show Globex Hi/Lo?", inline = dwmLevels, group = groupDwmLevels)
extendLines         = input.bool(defval = false, title = "Extend Levels Across Screen?", inline = dwmLevels, group = groupDwmLevels)
showDwmClose        = input.bool(defval = false, title = "Show Close?", inline = dwmLevels)
dwmLineWidth        = input.int(defval = 2, title = "Levels Line Width", minval = 1, maxval = 4, step = 1, group = groupDwmLevels)

showHTFsdZones   = input.bool(true, title = "Show HTF Supply/Demand Zones?", inline = sdZoneGroupTxt, group = groupSD)
showLTFsdZones   = input.bool(true, title = "Show LTF Supply/Demand Zones?", inline = sdZoneGroupTxt, group = groupSD)
sdColorLTF       = input.color(defval = #e7c6fd7a, title = 'LTF S/D Color', inline = "LTF Color", group = groupSD)
sdBorderColorLTF = input.color(defval = #e7c6fd7a, title = 'LTF S/D Border Color', inline = "LTF Color", group = groupSD)
sdColorD         = input.color(defval = #00a2ff1a, title = 'D S/D Color', inline = "D Color", group = groupSD)
sdBorderColorD   = input.color(defval = #00a2ff1a, title = 'D S/D Border Color', inline = "D Color", group = groupSD)
sdColorW         = input.color(defval = #fcf7001a, title = 'W S/D Color', inline = "W Color", group = groupSD)
sdBorderColorW   = input.color(defval = #fcf7001a, title = 'W S/D Border Color', inline = "W Color", group = groupSD)
sdColorM         = input.color(defval = #fc87011a, title = 'M S/D Color', inline = "M Color", group = groupSD)
sdBorderColorM   = input.color(defval = #fc87011a, title = 'M S/D Border Color', inline = "M Color", group = groupSD)

sdOffset        = input.int(defval = 2, minval = 0, maxval = 30, title="Extend Zones", group = groupSDSettings, tooltip = 'Extends Zone X number of bars past current price')
showZoneText    = input(true, title = "Show Zone Text", inline = "ZoneTxt", group = groupSDSettings)
sdZoneTextColor = input.color(defval = color.rgb(0, 0, 0), title = 'S/D Zone Text Color', group = groupSDSettings)
sdTextAlignV    = input.string("Top", title="Text Align (Vertical)", options=["Top", "Center", "Bottom"], group = groupSDSettings)
show2mZones     = input.bool(false, title="Show 2m", inline = "LTF", group = groupSDSettings)
show5mZones     = input.bool(false, title="Show 5m", inline = "LTF", group = groupSDSettings)
show15mZones    = input.bool(false, title="Show 15m", inline = "LTF", group = groupSDSettings)
show30mZones    = input.bool(false, title="Show 30m", inline = "ITF", group = groupSDSettings)
show60mZones    = input.bool(false, title="Show 60m", inline = "ITF", group = groupSDSettings)
show4hZones     = input.bool(false, title="Show 4h", inline = "ITF", group = groupSDSettings)
showDZones      = input.bool(true, title="Show D", inline = "HTF", group = groupSDSettings)
showWZones      = input.bool(true, title="Show W", inline = "HTF", group = groupSDSettings)
showMZones      = input.bool(true, title="Show M", inline = "HTF", group = groupSDSettings)

if sdTextAlignV == 'Bottom'
    sdTextAlignV := text.align_bottom
if sdTextAlignV == 'Center'
    sdTextAlignV := text.align_center
if sdTextAlignV == 'Top'
    sdTextAlignV := text.align_top

showCogs      = input.bool(true, title="Show Recent Cogs", inline = "Cogs", group = groupCog)
showAllCogs     = input.bool(false, title="Show All Cogs (up to last 50)", inline = "Cog", group = groupCog)

showAPlusPanel  = input.bool(false, title="Show A+ Setup Helper", inline = "Panel", group = groupAPlusPanel)

// EMAs -----------------------------
plot(outLtf, title="EMA-20", color = ltfColor, offset = 0, linewidth = 2, display = displaySet)
plot(displayMtf ? outMtf : na, title="EMA-50", color = mtfColor, offset = 0, linewidth = 2, display = displaySet)
plot(displayHtf or displayLtf ? outHtf : na, title="EMA-200", color = htfColor, offset = 0, linewidth = 2, display = displaySet)

// B BANDS --------------------------
p1Htf = plot(displayHtf ? upperHtf : na, "HTF Upper BBand", color = upperHtfColor, display = displaySet)
p2Htf = plot(displayHtf ? lowerHtf : na, "HTF Lower BBand", color = lowerHtfColor, display = displaySet)
p1Ltf = plot(displayVwapLtf and showBbLtf ? upperLtf : na, "LTF Upper Band", color = upperLtfColor, display = displaySet)
p2Ltf = plot(displayVwapLtf and showBbLtf ? lowerLtf : na, "LTF Lower Band", color = lowerLtfColor, display = displaySet)

// VWAP ---------------------------------
// If it's a short day, then there could be no daily bar. Take a previous one.
if na(dwmBarTime)
    dwmBarTime := nz(dwmBarTime[1])
var periodStartD = time - time // zero
var periodStartW = time - time // zero
var periodStartM = time - time // zero
var periodStartY = time - time // zero

makeMondayZero(dayOfWeek) => (dayOfWeek + 5) % 7

isMidnight(t) =>
    hour(t) == 0 and minute(t) == 0

isSameDay(t1, t2) =>
    dayofmonth(t1) == dayofmonth(t2) and
  month(t1) == month(t2) and
  year(t1) == year(t2)

//  isGlobex() =>
//      not (request.security(syminfo.tickerid, "D", isSameDay(time, time_close), barmerge.gaps_on, barmerge.lookahead_on))

tradingDayStart(t) =>
    y = year(t)
    m = month(t)
    d = dayofmonth(t)
    timestamp(y, m, d, 0, 0, 0)

numDaysBetween(time1, time2) =>
    y1 = year(time1)
    m1 = month(time1)
    d1 = dayofmonth(time1)
    
    y2 = year(time2)
    m2 = month(time2)
    d2 = dayofmonth(time2)
    
    diff = math.abs(timestamp("GMT", y1, m1, d1, 0, 0) - timestamp("GMT", y2, m2, d2, 0, 0))
    diff / MILLIS_IN_DAY

tradingDay = tradingDayStart(dwmBarTime)

isNewDPeriod() =>
    isNew = false
    if tradingDay != nz(tradingDay[1])
        isNew := na(tradingDay[1]) or tradingDay >= tradingDay[1]
    
    isNew

isNewWPeriod() =>
    isNew = false
    if tradingDay != nz(tradingDay[1])
        DAYS_IN_WEEK = 7
        if isFutures
            isNew := dayofweek(periodStartW) + numDaysBetween(periodStartW, tradingDay) >= DAYS_IN_WEEK
        else
            isNew := makeMondayZero(dayofweek(periodStartW)) + numDaysBetween(periodStartW, tradingDay) >= DAYS_IN_WEEK
            
    isNew

isNewMPeriod() =>
    isNew = false
    if tradingDay != nz(tradingDay[1])
        isNew := month(periodStartM) != month(tradingDay) or year(periodStartM) != year(tradingDay)
            
    isNew

isNewYPeriod() =>
    isNew = false
    if tradingDay != nz(tradingDay[1])
        isNew := year(periodStartY) != year(tradingDay) or year(periodStartY) != year(tradingDay)
            
    isNew

sumSrcD = float(na)
sumVolD = float(na)
sumSrcD := nz(sumSrcD[1], 0)
sumVolD := nz(sumVolD[1], 0)

sumSrcW = float(na)
sumVolW = float(na)
sumSrcW := nz(sumSrcW[1], 0)
sumVolW := nz(sumVolW[1], 0)

sumSrcM = float(na)
sumVolM = float(na)
sumSrcM := nz(sumSrcM[1], 0)
sumVolM := nz(sumVolM[1], 0)

sumSrcY = float(na)
sumVolY = float(na)
sumSrcY := nz(sumSrcY[1], 0)
sumVolY := nz(sumVolY[1], 0)

if isNewDPeriod()
    periodStartD := tradingDay
    sumSrcD := 0.0
    sumVolD := 0.0

if isNewWPeriod()
    periodStartW := tradingDay
    sumSrcW := 0.0
    sumVolW := 0.0

if isNewMPeriod()
    periodStartM := tradingDay
    sumSrcM := 0.0
    sumVolM := 0.0

if isNewYPeriod()
    periodStartY := tradingDay
    sumSrcY := 0.0
    sumVolY := 0.0    

if not na(hlc3) and not na(volume)
    sumSrcD := sumSrcD + hlc3 * volume
    sumVolD := sumVolD + volume

if not na(hlc3) and not na(volume)
    sumSrcW := sumSrcW + hlc3 * volume
    sumVolW := sumVolW + volume

if not na(hlc3) and not na(volume)
    sumSrcM := sumSrcM + hlc3 * volume
    sumVolM := sumVolM + volume

if not na(hlc3) and not na(volume)
    sumSrcY := sumSrcY + hlc3 * volume
    sumVolY := sumVolY + volume    

dVwapValue = sumSrcD / sumVolD
wVwapValue = sumSrcW / sumVolW
mVwapValue = sumSrcM / sumVolM
yVwapValue = sumSrcY / sumVolY
plot(not isForex ? dVwapValue : na, title="D VWAP", style = plot.style_circles, color=dvColor, linewidth = 2, display = displaySet)
plot(not isForex ? wVwapValue : na, title="W VWAP", style = plot.style_circles, color=wvColor, linewidth = 2, display = displaySet)
plot(not isForex ? mVwapValue : na, title="M VWAP", style = plot.style_circles, color=mvColor, display = displaySet)
plot(not isForex ? yVwapValue : na, title="Y VWAP", style = plot.style_circles, color=yvColor, display = displaySet)

// DWM / Globex PRICE LEVELS --------------------------
// show gLvlv? Update plot start for globex level and ray to right. replace 'time' with start time of new hi/lo
// if syminfo.type == 'futures' and showGlobexLevels
//     globex_High_Line     = line.new(Offset(10), _gH, _gT, _gH, xloc = xloc.bar_time, extend = extend.right, color = globex_Line_Color, style = line.style_solid, width = dwmLineWidth)
//     line.delete(globex_High_Line[1])
//     globex_Low_Line      = line.new(Offset(10), _gL, _gT, _gL, xloc = xloc.bar_time, extend = extend.right, color = globex_Line_Color, style = line.style_solid, width = dwmLineWidth)
//     line.delete(globex_Low_Line[1])

daily_High_Line     = line.new(time, _dH, time + 1, _dH, xloc = xloc.bar_time, extend = extendLines ? extend.both : extend.right, color = d_Line_Color, style = line.style_dotted, width = dwmLineWidth)
line.delete(daily_High_Line[1])
daily_Low_Line      = line.new(time, _dL, time + 1, _dL, xloc = xloc.bar_time, extend = extendLines ? extend.both : extend.right, color = d_Line_Color, style = line.style_dotted, width = dwmLineWidth)
line.delete(daily_Low_Line[1])
daily_Close_Line    = line.new(time, showDwmClose ? _dC : na, time + 1, _dC, xloc = xloc.bar_time, extend = extendLines ? extend.both : extend.right, color = d_Line_Color, style = line.style_dotted, width = dwmLineWidth)
line.delete(daily_Close_Line[1])

weekly_High_Line    = line.new(time, _wH, time + 1, _wH, xloc = xloc.bar_time, extend = extendLines ? extend.both : extend.right, color = w_Line_Color, style = line.style_dotted, width = dwmLineWidth)
line.delete(weekly_High_Line[1])
weekly_Low_Line     = line.new(time, _wL, time + 1, _wL, xloc = xloc.bar_time, extend = extendLines ? extend.both : extend.right, color = w_Line_Color, style = line.style_dotted, width = dwmLineWidth)
line.delete(weekly_Low_Line[1])
weekly_Close_Line   = line.new(time, showDwmClose ? _wC : na,  time + 1, _wC,  xloc = xloc.bar_time, extend = extendLines ? extend.both : extend.right, color = w_Line_Color, style = line.style_dotted, width = dwmLineWidth)
line.delete(weekly_Close_Line[1])

monthly_High_Line   = line.new(time, _mH,  time + 1, _mH,  xloc = xloc.bar_time, extend = extendLines ? extend.both : extend.right, color = m_Line_Color, style = line.style_dotted, width = dwmLineWidth)
line.delete(monthly_High_Line[1])
monthly_Low_Line    = line.new(time, _mL,   time + 1, _mL,   xloc = xloc.bar_time, extend = extendLines ? extend.both : extend.right, color = m_Line_Color, style = line.style_dotted, width = dwmLineWidth)
line.delete(monthly_Low_Line[1])
monthly_Close_Line  = line.new(time, showDwmClose ? _mC : na, time + 1, _mC, xloc = xloc.bar_time, extend = extendLines ? extend.both : extend.right, color = m_Line_Color, style = line.style_dotted, width = dwmLineWidth)
line.delete(monthly_Close_Line[1])

// label_globex_High    = label.new(bar_index, _gH, text = globexBlank + "Globex Hi : " + str.tostring(_gH, format.mintick), color = color.new(color.black, 100), style = label.style_label_left, textcolor = globex_Line_Color, size = size.small, textalign = text.align_center)
// label.delete(label_globex_High[1])
// label_globex_Low     = label.new(bar_index, _gL, text = globexBlank + "Globex Lo : "  +  str.tostring(_gL, format.mintick), color = color.new(color.black, 100), style = label.style_label_left, textcolor = globex_Line_Color, size = size.small, textalign = text.align_center)
// label.delete(label_globex_Low[1])

label_daily_High    = label.new(bar_index, _dH, text = dBlank + "Prev D High : " + str.tostring(_dH, format.mintick), color = color.new(color.black, 100), style = label.style_label_left, textcolor = d_Line_Color, size = size.small, textalign = text.align_center)
label.delete(label_daily_High[1])
label_daily_Low     = label.new(bar_index, _dL, text = dBlank + "Prev D Low : "  +  str.tostring(_dL, format.mintick), color = color.new(color.black, 100), style = label.style_label_left, textcolor = d_Line_Color, size = size.small, textalign = text.align_center)
label.delete(label_daily_Low[1])
label_daily_Close   = label.new(bar_index, showDwmClose ? _dC : na,   text = dBlank + "Prev D Close : " +  str.tostring(_dC, format.mintick), color = color.new(color.black, 100), style = label.style_label_left, textcolor = d_Line_Color, size = size.small, textalign = text.align_center)
label.delete(label_daily_Close[1])

label_weekly_High   = label.new(bar_index, _wH, text = wBlank + "Prev W High : " + str.tostring(_wH, format.mintick), color = color.new(color.black, 100), style = label.style_label_left, textcolor = w_Line_Color, size = size.small, textalign = text.align_center)
label.delete(label_weekly_High[1])
label_weekly_Low    = label.new(bar_index, _wL, text = wBlank + "Prev W Low : "  + str.tostring(_wL, format.mintick), color = color.new(color.black, 100), style = label.style_label_left, textcolor = w_Line_Color, size = size.small, textalign = text.align_center)
label.delete(label_weekly_Low[1])
label_weekly_Close  = label.new(bar_index, showDwmClose ? _wC : na,  text = wBlank + "Prev W Close : " + str.tostring(_wC, format.mintick), color = color.new(color.black, 100), style = label.style_label_left, textcolor = w_Line_Color, size = size.small, textalign = text.align_center)
label.delete(label_weekly_Close[1])

label_monthly_High  = label.new(bar_index, _mH, text = mBlank + "Prev M High : " + str.tostring(_mH, format.mintick) + "\n", color = color.new(color.black, 100), style = label.style_label_left, textcolor = m_Line_Color, size = size.small, textalign = text.align_center)
label.delete(label_monthly_High[1])
label_monthly_Low   = label.new(bar_index, _mL, text = mBlank + "Prev M Low : "  + str.tostring(_mL, format.mintick) + "\n", color = color.new(color.black, 100), style = label.style_label_left, textcolor = m_Line_Color, size = size.small, textalign = text.align_center)
label.delete(label_monthly_Low[1])
label_monthly_Close = label.new(bar_index, showDwmClose ? _mC : na, text = mBlank + "Prev M Close : " + str.tostring(_mC, format.mintick) + "\n", color = color.new(color.black, 100), style = label.style_label_left, textcolor = m_Line_Color, size = size.small, textalign = text.align_center)
label.delete(label_monthly_Close[1])

// Supply and Demand Zones -----------------------------------------
if tf == 'D'
    tf := '1440'
if tf == 'W'
    tf := '10080'
if showLTFsdZones
    tf := '1'

momentCTD           = math.round(time(tf) + (sdOffset * 60000 * str.tonumber(tf)))
var box[] supply_HT = array.new_box()
var box[] demand_HT = array.new_box()

drawSupplyZone(formattedTf, ctd_ht, top_ht, ctd, bot_ht, box_color, border_color) =>
    box supply = box.new(left=ctd_ht, top=top_ht, right=ctd, bgcolor=box_color, bottom=bot_ht, xloc=xloc.bar_time)
    box.set_border_color(supply, border_color)
    if showZoneText
        box.set_text(supply, formattedTf)
        box.set_text_size(supply, size.small)
        box.set_text_color(supply, sdZoneTextColor)
        box.set_text_halign(supply, text.align_right)
        box.set_text_valign(supply, sdTextAlignV)
    array.push(supply_HT, supply)

drawDemandZone(formattedTf, ctd_ht, top_ht, ctd, bot_ht, box_color, border_color) =>
    box demand = box.new(left=ctd_ht, top=top_ht, right=ctd, bottom=bot_ht, bgcolor=box_color, xloc=xloc.bar_time)
    box.set_border_color(demand, border_color)
    if showZoneText
        box.set_text(demand, formattedTf)
        box.set_text_size(demand, size.small)
        box.set_text_color(demand, sdZoneTextColor)
        box.set_text_halign(demand, text.align_right)
        box.set_text_valign(demand, sdTextAlignV)
    array.push(demand_HT, demand)

// drawZone(formattedTf, left_ctd_ht, top_ht, ctd_ht, bot_ht, box_color, border_color) =>


createZones(timeframe) => 
    [open_HT, high_HT, low_HT, close_HT]    = request.security(syminfo.tickerid, timeframe, [open[1], high[1], low[1], close[1]], lookahead = barmerge.lookahead_on)

    redCandle_HT        = close_HT < open_HT
    greenCandle_HT      = close_HT > open_HT
    neutralCandle_HT    = close_HT == open_HT
    candleChange_HT     = math.abs(close_HT - open_HT)

    formatTf            = ""
    bgColorForBox       = sdColorLTF
    borderColorForBox   = sdBorderColorLTF

    if timeframe == '2'
        formatTf := '2m'
    else if timeframe == '5'
        formatTf := '5m'
    else if timeframe == '15'
        formatTf := '15m'
    else if timeframe == '30'
        formatTf := '30m'
    else if timeframe == '45'
        formatTf := '45m'
    else if timeframe == '60'
        formatTf := '1h'
    else if timeframe == '240'
        formatTf := '4h'
    else if timeframe == 'D'
        formatTf := 'D'
        bgColorForBox := sdColorD
        borderColorForBox := sdBorderColorD
    else if timeframe == 'W'
        formatTf := 'W'
        bgColorForBox := sdColorW
        borderColorForBox := sdBorderColorW
    else if timeframe == 'M'
        formatTf := 'M'
        bgColorForBox := sdColorM
        borderColorForBox := sdBorderColorM    
    // test M    

    var float bottomBox_HT = na
    var float topBox_HT = na
    momentCTD_HT = time(timeframe)[2]

    if (((redCandle_HT and greenCandle_HT[1]) or (redCandle_HT and neutralCandle_HT[1])) and (candleChange_HT / candleChange_HT[1]) >= legOutCandleSize and barstate.isconfirmed[1] and (showHTFsdZones or showLTFsdZones) and close_HT[1] >= close_HT and open_HT[1] <= open_HT)

        if high_HT >= high_HT[1]
            topBox_HT := high_HT
        else 
            topBox_HT := high_HT[1]

        drawSupplyZone(formatTf, momentCTD_HT, topBox_HT, momentCTD, open_HT[1], bgColorForBox, borderColorForBox)
  
    if (((greenCandle_HT and redCandle_HT[1]) or (greenCandle_HT and neutralCandle_HT[1])) and (candleChange_HT / candleChange_HT[1]) >= legOutCandleSize and barstate.isconfirmed[1] and (showHTFsdZones or showLTFsdZones) and close_HT[1] <= close_HT and open_HT[1] >= open_HT)

        if low_HT <= low_HT[1]
            bottomBox_HT := low_HT
        else 
            bottomBox_HT := low_HT[1]

        drawDemandZone(formatTf, momentCTD_HT, open_HT[1], momentCTD, bottomBox_HT, bgColorForBox, borderColorForBox)

if show2mZones and str.tonumber(tf) <= 2
    createZones('2')
if show5mZones and str.tonumber(tf) <= 5
    createZones('5')
if show15mZones and str.tonumber(tf) <= 15
    createZones('15')
if show30mZones and str.tonumber(tf) <= 30
    createZones('30')
if show60mZones and str.tonumber(tf) <= 60
    createZones('60')
if show4hZones and str.tonumber(tf) <= 240
    createZones('240') 
if showDZones and str.tonumber(tf) <= 1440
    createZones('D')
if showWZones and str.tonumber(tf) <= 10080
    createZones('W')
if showMZones and str.tonumber(tf) <= 43800
    createZones('M')

i = 0
while i < array.size(supply_HT) and array.size(supply_HT) > 0
    box currentBox = array.get(supply_HT, i)
    float breakLevel = box.get_top(currentBox)
    if high > breakLevel 
        array.remove(supply_HT, i)
        box.delete(currentBox)
        int(na)
    else
        box.set_right(currentBox, momentCTD)
        i += 1
        int(na)

i2 = 0
while i2 < array.size(demand_HT) and array.size(demand_HT) > 0
    box currentBox = array.get(demand_HT, i2)
    float breakLevel = box.get_bottom(currentBox)
    if low < breakLevel 
        array.remove(demand_HT, i2)
        box.delete(currentBox)
        int(na)
    else
        box.set_right(currentBox, momentCTD)
        i2 += 1
        int(na)

// COG Finder --------------------------------
// [curr_candle_o, curr_candle_h, curr_candle_l, curr_candle_c, prev_candle_o, prev_candle_h, prev_candle_l, prev_candle_c]    = request.security(syminfo.tickerid, tf, [open, high, low, close, open[1], high[1], low[1], close[1]], lookahead = barmerge.lookahead_on)

// isGreenCandle       = curr_candle_c > curr_candle_o
// isRedCandle         = curr_candle_c < curr_candle_o
// isDoji              = curr_candle_c == curr_candle_o
// isPrevGreenCandle   = prev_candle_c > prev_candle_o
// isPrevRedCandle     = prev_candle_c < prev_candle_o
// isPrevDoji          = prev_candle_c == prev_candle_o
// _pos_cog            = (isGreenCandle and isPrevRedCandle) and (curr_candle_c >= prev_candle_h) and (not isDoji) and (curr_candle_c > outLtf)
// _neg_cog            = (isRedCandle and isPrevGreenCandle) and (curr_candle_c <= prev_candle_l) and (not isDoji) and (curr_candle_c < outLtf)

// plotshape(_neg_cog, title="", style=shape.arrowdown, location=location.abovebar, color=color.red, size=size.large)
// plotshape(_neg_cog, title="", style=shape.arrowdown, location=location.abovebar, color=color.red, size=size.large)

C_ShadowPercent = 5.0 // size of shadows
C_ShadowEqualsPercent = 100.0
C_DojiBodyPercent = 5.0
C_Factor = 1.0 // shows the number of times the shadow dominates the candlestick body

C_BodyHi = math.max(close, open)
C_BodyLo = math.min(close, open)
C_Body = C_BodyHi - C_BodyLo
C_UpShadow = high - C_BodyHi
C_DnShadow = C_BodyLo - low
C_HasUpShadow = C_UpShadow > C_ShadowPercent / 100 * C_Body
C_HasDnShadow = C_DnShadow > C_ShadowPercent / 100 * C_Body
C_WhiteBody = open < close
C_BlackBody = open > close
C_Range = high-low
C_IsInsideBar = C_BodyHi[1] > C_BodyHi and C_BodyLo[1] < C_BodyLo
C_BodyMiddle = C_Body / 2 + C_BodyLo
C_ShadowEquals = C_UpShadow == C_DnShadow or (math.abs(C_UpShadow - C_DnShadow) / C_DnShadow * 100) < C_ShadowEqualsPercent and (math.abs(C_DnShadow - C_UpShadow) / C_UpShadow * 100) < C_ShadowEqualsPercent
C_IsDojiBody = C_Range > 0 and C_Body <= C_Range * C_DojiBodyPercent / 100
C_Doji = C_IsDojiBody and C_ShadowEquals

patternLabelPosLow = low - (ta.atr(30) * 0.6)
patternLabelPosHigh = high + (ta.atr(30) * 0.6)

label_color_bearish = input(color.red, "Label Color Bearish")
C_EngulfingBearishNumberOfCandles = 1
C_EngulfingBearish = C_BlackBody and C_WhiteBody[1] and close <= open[1] and open >= close[1] and ( close < open[1] or open > close[1] ) and close < outLtf and close < dVwapValue
bgcolor(ta.highest(C_EngulfingBearish and showCogs ?  1 : 0, C_EngulfingBearishNumberOfCandles) != 0 ? color.new(color.red, 90) : na, offset =- (C_EngulfingBearishNumberOfCandles - 1))